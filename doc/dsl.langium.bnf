<ArduinoMl> ::= "app" <ID> "initial" "state" <ID> "{"
                  "bricks" <Brick>+
                  "states" <State>+
               "}"

<Brick> ::= <Actuator> | <Sensor>

<Actuator> ::= "Actuator" <ID> ":" <PIN_INT>

<Sensor> ::= "Sensor" <ID> ":" <PIN_INT>

<State> ::= <ID> "{" <NormalState> | <ErrorState> "}"

<NormalState> ::= (<PerpetualState> | <TemporalState>)

<PerpetualState> ::= ((<Action>)+) ((<ConditionalTransition>)+) 

<TemporalState> ::= ((<Action>)+) <TemporalTransition> ((<ConditionalTransition>)+) 

<ErrorState> ::= "ErrorState" "errorNumber" ":" <INT>
                "pauseTime" ":" <INT>
                "errorLed" ":" <ID>
                (<ConditionalTransition> (<ConditionalTransition>)*) 

<Action> ::= <ID> "<=" <Signal>

<LogicalOperator> ::= "and" | "or" | "xor"

<NegationOperator> ::= "not"

<ConditionalTransition> ::= <Condition> "=>" <ID>

<TemporalTransition> ::= "after" <INT> "ms" "=>" <ID> | "runFor" <INT> "ms"

<Condition> ::= <SignalCondition> | <CompositeCondition>

<SignalCondition> ::= <ID> "is" (<NegationOperator>)? <Signal>

<CompositeCondition> ::= "(" <Condition> <LogicalOperator> <Condition> ")"

<Signal> ::= "HIGH" | "LOW"

<ID> ::= ( <letter> | <digit> | "_" )+

<INT> ::= <digit>+

<PIN_INT> ::= <digit> | (<digit> <limitedDigit>)

<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | 
            "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<limitedDigit> ::= "0" | "1" | "2" | "3" 
